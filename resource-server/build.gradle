plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'grpc'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// Spring Boot 스타터 의존성
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// 검증 관련 의존성
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Lombok 의존성 (컴파일 타임 어노테이션 처리)
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// 데이터베이스 연결 관련 의존성
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// 테스트 관련 의존성
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// JUnit 5 런처
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Swagger (API 문서화)
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	// gRPC
	implementation 'io.grpc:grpc-netty-shaded:1.66.0'
	implementation 'io.grpc:grpc-protobuf:1.66.0'
	implementation 'io.grpc:grpc-stub:1.66.0'
	implementation "com.google.protobuf:protobuf-java:4.27.4"
	implementation "com.google.protobuf:protobuf-java-util:4.27.4"
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	// gRPC Client
	implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'
}

// gRPC 및 Protobuf 설정
protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:4.27.4"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.66.0"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
			}
		}
	}
}
// 프로토콜 버퍼 파일 경로 설정
sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
		java {
			// 프로토콜 파일로부터 생성된 코드 경로를 포함
			srcDirs += 'build/generated/source/proto/main/java'
			srcDirs += 'build/generated/source/proto/main/grpc'
		}
	}
}

// 컴파일 태스크가 Protobuf 파일 생성을 포함하도록 의존성 설정
compileJava {
	dependsOn 'generateProto'
}

tasks.processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('test') {
	useJUnitPlatform()
}
